import "../../Header Files/model/puppet.del";
import "model.ostw";
import "../hud.ostw";
rule: 'Puppet Setup'
Event.OngoingPlayer
if(IsDummyBot())
if(getPlayerInfo(EPlayerInfo.CAN_SETUP))
{
    heroSize = heroSizes[IndexOfArrayValue(AllHeroes(), HeroOf())];
    define size: 0.9;
    define healthBarSocket: UpdateEveryFrame(EyePosition()) + Vector(0,0.27,0.44); //+ Vector(0,0.22,0.39); //0.45 
    define healthBarTitleSocket: UpdateEveryFrame(EyePosition()) + Vector(0,0.46,0.56);//Vector(0,0.35,0.55); //0.7
    printToScreen(EventPlayer().health);
    setName(HeroOf());
    define visibility: (IsAlive() && getPlayerInfo(EPlayerInfo.IS_READY)) ? (getPlayerInfo(EPlayerInfo.VISIBLE) ? LocalPlayer() : AllPlayers(TeamOf())) : null;
    define teamSpecificColor: LocalPlayer() == EventPlayer() ? Color.Green : getPlayerInfo(EPlayerInfo.TEAM_NUM) == 0 ? Color.Team1 : Color.Team2;
    define bufferChars: StringSlice("＿＿＿", 0, getPlayerInfo(EPlayerInfo.NAME_BUFFER_CHARS));
    CreateInWorldText(visibility, $"{bufferChars}{getPlayerInfo(EPlayerInfo.NAME_STRING)}{bufferChars}", healthBarTitleSocket, size, Clipping.DoNotClip, InworldTextRev.VisibleToPositionStringAndColor, teamSpecificColor, Spectators.DefaultVisibility);
    CreateInWorldText(visibility, healthbarString[1], healthBarSocket, size, Clipping.DoNotClip, InworldTextRev.VisibleToPositionStringAndColor, teamSpecificColor, Spectators.DefaultVisibility);
    CreateInWorldText(visibility, healthbarString[0], healthBarSocket, size, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString, Color.White, Spectators.DefaultVisibility);
    health = 0;
    WaitUntil(HasSpawned(), 9999);
    maxHealth = MaxHealth();
    Wait();
    updateHealthBar();
    while(true){
        StartThrottleInDirection(EventPlayer(), Left(), 200/maxHealth, Relative.ToPlayer, ThrottleBehavior.ReplaceExistingThrottle, ThrottleRev.None);
        while(health < maxHealth){
            health += 5;
            updateHealthBar();
            Wait(0.02);
        }
        maxHealth += 25;
        StartThrottleInDirection(EventPlayer(), Right(), 200/maxHealth, Relative.ToPlayer, ThrottleBehavior.ReplaceExistingThrottle, ThrottleRev.None);
        while(health > 0){
            health -= 5;
            updateHealthBar();
            Wait(0.02);
            
        }
        maxHealth += 25;
    }
}

rule: 'When Ready'
Event.OngoingPlayer
if(IsDummyBot())
if(READY)
{
    WaitUntil(getPlayerInfo(EPlayerInfo.CAN_SETUP), 1000);
    setPlayerInfo(EPlayerInfo.IS_READY, true);
    setPlayerInfo(EPlayerInfo.VISIBLE, true);
}

